apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.lachlanmckee'
version = '1.0.0'

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile "junit:junit:$junit_version"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'junit-runner-ex'
                    packaging 'jar'
                    description 'A suite of customisable junit 4 test suites'
                    url 'http://lachlanmckee.net/junit-runner-ex/'

                    scm {
                        url 'scm:git@github.com:LachlanMcKee/junit-runner-ex.git'
                        connection 'scm:git@github.com:LachlanMcKee/junit-runner-ex.git'
                        developerConnection 'scm:git@github.com:LachlanMcKee/junit-runner-ex.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'lachlanmckee'
                            name 'Lachlan McKee'
                        }
                    }
                }
            }
        }
    }
}